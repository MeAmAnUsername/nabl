module statix/test/evaluate

imports

  nabl2/api
  
  signatures/statix/lang/-
  statix/lang/-
  statix/lang/statics/-
  statix/lang/normalize

  statix/runtime/-

  signatures/statix/test/-
  pp/statix/test/-
  statix/test/-
  
  statix/api
  statixruntime
  statix/runtime/-

rules
  
  editor-evaluate:
      (_, _, Module(_, _), path, project-path) -> None()

  editor-evaluate:
      (_, _, ast@Test(_, _), path, project-path) -> (filename, result)
    with result   := <evaluate;pp-Statix-string(prettyprint-Statix-TestResult)> ast;
         filename := <guarantee-extension(|"stxresult")> path

rules

  editor-evaluation-pair:
      (_, _, Module(_, _), path, project-path) -> None()

  editor-evaluation-pair:
      (_, _, ast@Test(_, _), path, project-path) -> (filename, result)
    with result   := <build-evaluation-pair> ast;
         filename := <guarantee-extension(|"evalpair.aterm")> path

  build-evaluation-pair: ast@Test(_, _) -> (body', spec')
    with a      := <nabl2-get-ast-analysis> ast;
         CustomAnalysis(ext) := <nabl2-get-custom-analysis> a;
         ast'   := <normalize(|a)> ast;
         Test(body, _) := ast';
         spec   := <core-ast-to-spec-aterm(|a)> (ast', ext);
         spec'  := <stx--local-spec-from-spec> spec;
         body'  := <qualify-names(|a);eliminate-all;stx--index-ast(|""/*path*/)> body

signature
  constructors
    SUCCEEDS : TestResult
    FAILS    : TestResult
    WARNS    : TestResult

rules

  evaluate-test: ast -> result
    with if EvalResult(_, [], w*, n*) := <evaluate> ast then
           if [] := w* then
             result := SUCCEEDS()
           else
             result := WARNS()
           end
         else
           result := FAILS()
         end

  evaluate: ast@Test(_, _) -> result
    with a      := <nabl2-get-ast-analysis> ast;
         CustomAnalysis(ext) := <nabl2-get-custom-analysis> a;
         ast'   := <normalize(|a)> ast;
         Test(body, _) := ast';
         spec   := <core-ast-to-spec-aterm(|a)> (ast', ext);
         spec'  := <stx--local-spec-from-spec> spec;
         level  := <stx--log-level>;
         body'  := <qualify-names(|a);eliminate-all;stx--index-ast(|""/*path*/)> body;
         s@Solution(_, a') := <stx--solve-constraint(|spec', level)> body';
         (error*, warning*, note*) := <stx--delays-as-errors;stx--extract-messages> a';
         result := EvalResult(s, error*, warning*, note*)
