module statix/runtime/analysis

imports

  libstratego-lib

  libspoofax/analysis/constraint
  libspoofax/core/language
  libspoofax/resource/path

  signatures/statix/runtime/-
  pp/statix/runtime/-

  statix/runtime/ast
  statix/runtime/constraints
  statix/runtime/terms
  statix/runtime/pp

  statix/api

signature
  constructors

    : List(String) -> Key

    ProjectAnalysis : /*globalScope*/Scope * /*globalAnalysis*/SolverResult * /*initialAnalysis*/SolverResult * /*finalAnalysis*/SolverResult -> AnalysisResult
    FileAnalysis    : /*initialAnalysis*/SolverResult * /*finalAnalysis*/SolverResult -> AnalysisResult

    FileSpec : List(String) * List(Label) * List(Label) * Label * List(Rule) * List(ID, Integer, Label) -> Spec
    Spec     : List(Label) * List(Label) * Label * List(Rule) * List(ID, Integer, Label) -> Spec
    Solution : List(Var, Term) * SolverResult -> Solution

    WithConfig : Config * Term -> WithConfig

    EOP : Label // duplicate from statix.lang

signature
  constructors // Concurrent/Incremental

    GroupKey        : String * Term -> GroupKey
    Key             : List(GroupKey) * String * Unit -> Key
    CompositeUnit   : Term * List(GroupKey, Unit) -> Unit
    Unit            : Term -> Unit

    Project : /*resource*/String * /*ast*/Term * /*cached result*/UnitResult * /*subGroups*/List(String, StatixGroup) * /*subUnits*/List(String, StatixUnit) * /*libraries*/List(String, StatixLibrary) -> StatixProject
    Library : /*rootScopes*/List(Scope) * /*ownScopes*/List(Scope) * /*scopeGraph*/ScopeGraph -> StatixLibrary
    Group   : /*resource*/String * /*ast*/Term * /*cached result*/UnitResult * /*subGroups*/List(String, StatixGroup) * /*subUnits*/List(String, StatixUnit) -> StatixGroup
    Unit    : /*resource*/String * /*ast*/Term * /*cached result*/UnitResult -> StatixUnit

    // Tasks

    ProjectTask     : String * Constraint * ProjectResult -> ProjectTask
    GroupTask       : Key * Constraint * UnitState -> GroupTask
    FileTask        : String * Key * Term * SubFileTask -> FileTask
    SimpleFileTask  : Constraint * UnitState -> SubFileTask
    CompositeTask   : Constraint * UnitState * List(String, SubFileTask) -> SubFileTask

    // TODO: remove
    Added           : InitialState
    Cached          : UnitResult -> InitialState
    Changed         : UnitResult -> InitialState

    Cached 	        : UnitState
    Changed         : UnitState

    // Results
    ProjectAnalysis : Analysis * UnitResult -> AnalysisResult
    FileAnalysis    : Key * FileTask * AST * SubAnalysis -> AnalysisResult
    SubAnalysis     : Constraint * Analysis * UnitResult * List(String, FileAnalysis) -> SubAnalysis

    // cstx solver outputs
    ProjectResult   : Analysis * UnitResult -> Result
    GroupResult     : Analysis * UnitResult -> Result
    UnitResult      : Analysis * UnitResult -> Result


rules // single file analysis

  stx--editor-analyze(pre, post|spec-name, init-constraint):
      AnalyzeSingle(change*, progress, cancel) -> AnalysisResult(result*)
    with spec  := <stx--language-spec-by-name> spec-name
       ; level := <stx--log-level>
       ; c-name := <stx--mk-qualified-name(|spec-name)> init-constraint
       ; config := <stx--project-config>
    with
      resource-ast* := <filter((id, ?Added(<id>) + ?Changed(<id>, _)))> change*
    ; resource-ast'* := <map(\ (resource, ast) -> (resource, <pre;stx--index-ast(|resource)> ast) \)> resource-ast*
    ; C* := <map(\ (resource, ast') -> C(c-name, [<stx--explode> ast'], NoMessage()) \)> resource-ast'*
    ; solve-result* := <stx--solve-constraint(|spec, level, progress, cancel)> C*
    ; result* := <zip({ ast'', file-analysis, error*, warning*, note*: \
        ((resource, ast'), Solution(_, a)) ->
            (resource, Full(ast'', file-analysis, error*, warning*, note*))
          with (error*, warning*, note*) := <stx--delays-as-errors(|config);stx--extract-messages(|config)> a
             ; ast'' := <post> ast'
             ; file-analysis := FileAnalysis(a, a)
      \})> (resource-ast'*, solve-result*)

  stx--editor-analyze(pre, post|spec-name, init-constraint):
      AnalyzeMulti(_, _, _, _) -> <fatal-err-msg(|"Single-file `stx-editor-analyze_1_2` called for multi-file analysis. Use `stx-editor-analyze_1_3` instead.")>

rules // single file elaboration

  stx--editor-elaborate(pre, post|spec-name, init-constraint):
      AnalyzeSingle(change*, progress, cancel) -> AnalysisResult(result*)
    with spec  := <stx--language-spec-by-name> spec-name
       ; level := <stx--log-level>
       ; c-name := <stx--mk-qualified-name(|spec-name)> init-constraint
       ; config := <stx--project-config>
    with
      resource-ast* := <filter((id, ?Added(<id>) + ?Changed(<id>, _)))> change*
    ; resource-ast'* := <map(\ (resource, ast) -> (resource, <pre;stx--index-ast(|resource)> ast) \)> resource-ast*
    ; V := Var("out")
    ; C* := <map(\ (resource, ast') -> <stx--exists>([V], C(c-name, [<stx--explode> ast', V], NoMessage())) \)> resource-ast'*
    ; solve-result* := <stx--solve-constraint(|spec, level, progress, cancel)> C*
    ; result* := <zip({ ast'', file-analysis, error*, warning*, note*: \
        ((resource, ast'), Solution(subst, a)) ->
            (resource, Full(ast'', file-analysis, error*, warning*, note*))
          with (error*, warning*, note*) := <stx--delays-as-errors(|config);stx--extract-messages(|config)> a
          with ast'' := <substitute(stx--is-var);stx--implode;post> (subst, V)
             ; file-analysis := FileAnalysis(a, a)
      \})> (resource-ast'*, solve-result*)

rules // multi file analysis (new)

  stx--no-grouping: (resource, ast) -> [(key, ast)]
    with key := [<current-language-relative-source-or-include-path> resource]

  cstx--editor-analyze(pre, group-key, post|spec-name, project-constraint, group-constraint, file-constraint):
      AnalyzeMulti((project-resource, project-change), file-change*, progress, cancel) -> AnalysisResult(result*)
    with spec  := <stx--language-spec-by-name> spec-name
       ; libs  := <stx--local-libs>
       ; level := <stx--log-level>
       ; proj-c-name := <stx--mk-qualified-name(|spec-name)> project-constraint
       ; grp-c-name  := <stx--mk-qualified-name(|spec-name)> group-constraint
       ; file-c-name := <stx--mk-qualified-name(|spec-name)> file-constraint
       ; s-proj      := Var("s_proj")
       ; s-grp       := Var("s_grp")
       ; config 	 := <stx--project-config>
       ; solver-mode := <stx--solver-mode>
    with
      //
      // 1. Build project task
      //
      project-ast  := ()
    ; project-rule := LLam([s-proj], C(proj-c-name, [s-proj], NoMessage()))
    ; project-res  := <?Cached(ProjectAnalysis(_, <!Cached(<id>)>)) + ?Changed(_, ProjectAnalysis(_, <!Changed(<id>)>)) + ?Added(<!Added()>)> project-change
    ; project-task := ProjectTask(project-resource, project-rule, project-res)
      //
      // 2. Build file tasks
      //
    ; file-task* := <map(cstx--make-file(pre,group-key|grp-c-name,file-c-name))> file-change*
      //
      // 4. Solve project
      //
    ; project   := <cstx--make-project(|grp-c-name, libs)> (project-task, file-task*)
    ; analysis* := <cstx--solve-multi(|solver-mode, spec, level, progress, cancel)> project
      //
      // 5. Construct results
      //
    ; project-result := <cstx--mk-project-result(|config, analysis*)> (project-resource, project-ast, project-change)
    ; file-result*   := <map(cstx--mk-file-result(post|config, file-task*, analysis*))> file-change*
    ; result* := [project-result|file-result*]
      //
      // 6. Compute scope graph diff
      //
  // Disabled as long as we do not know the root scope here
  //; if Cached(ProjectAnalysis(_, project-analysis')) := project-change then
  //    project-analysis := <lookup> (project-resource, analysis*)
  //  ; diff := <stx--diff-scopegraphs(|global-scope)> (project-analysis, project-analysis')
  //  ; <pp-Statix-string(prettyprint-Statix-Stx-Diff);debug> diff
  //  end

rules // cstx -- file tasks

  cstx--make-file(pre,group-key|group-c-name, file-c-name): (_, change) -> (group-key, task')
    where Cached(FileAnalysis(Key(group-key, _, _), task, _, sub-analysis)) := change
    with task' := <cstx--update-initial-state> task

  cstx--make-file(pre,group-key|group-c-name, file-c-name): (resource, change) -> (key*, task)
    with ast                   := <(?Added(<id>) + ?Changed(<id>, _)); pre; stx--index-ast(|resource)> change
       ; Key(key*, name, unit) := <cstx--composite-group-key(group-key)> (resource, ast)
       ; prev-res              := <?Changed(_, <id>) <+ !()>
       ; task                  := <cstx--file-unit-to-task(|group-c-name, file-c-name)> (resource, key*, ast, name, unit, prev-res)

  cstx--file-unit-to-task(|group-c-name, file-c-name): (resource, key*, ast, name, u, prev-res) -> task
    with prev-res' := <?FileAnalysis(_, _, _, <id>) + ?()> prev-res
       ; sub-task  := <cstx--file-subunit-to-task(|group-c-name, file-c-name)> (u, prev-res')
       ; task      := FileTask(resource, Key(key*, name, u), ast, sub-task)

  cstx--file-subunit-to-task(|group-c-name, file-c-name): (Unit(ast), prev-res) -> task
    with prev-ast   := <cstx--unit-term <+ !()> prev-res
       ; init-state := <cstx--initial-state(|prev-ast, ast)> prev-res
       ; s-parent   := <mk-s-parent>
       ; constraint := LLam([s-parent], C(file-c-name, [s-parent, <stx--explode> ast], NoMessage()))
       ; task       := SimpleFileTask(constraint, init-state)

  cstx--file-subunit-to-task(|group-c-name, file-c-name): (CompositeUnit(ast, sub-group*), prev-res) -> task
    with prev-ast   := <cstx--unit-term <+ !()> prev-res
       ; init-state := <cstx--initial-state(|prev-ast, ast)> prev-res
       ; s-parent   := <mk-s-parent>
       ; s-group    := <mk-s-group>
       ; constraint := LLam([s-parent, s-group], C(group-c-name, [s-parent, <stx--explode> ast, s-group], NoMessage()))
       ; sub-task*  := <map({name, unit, prev-res', task' : \ (name, unit) -> task'
                         with prev-res' := <cstx--sub-result(|name)> prev-res
                            ; task'     := <cstx--file-subunit-to-task(|group-c-name, file-c-name)> (unit, prev-res')
                       \})> sub-group*
       ; task       := CompositeTask(constraint, init-state, sub-task*)

  cstx--update-initial-state: FileTask(resource, key, ast, task) -> FileTask(resource, key, ast, task')
    with task' := <cstx--update-sub-initial-state> task

  cstx--update-sub-initial-state : SimpleFileTask(constraint, _) -> SimpleFileTask(constraint, Cached())
  cstx--update-sub-initial-state : CompositeTask(constraint, _, sub-task*) -> CompositeTask(constraint, Cached(), new-sub-task*)
    with new-sub-task* := <map(!(<id>, <cstx--update-sub-initial-state>))> sub-task*

  cstx--composite-group-key(group-key): (res, ast) -> key
    with raw-key := <group-key> (res, ast)
       ; if <map(is-string)> raw-key then
             (group-key, name) := <split-init-last> raw-key
           ; key := Key(<map(\x -> GroupKey(x, x)\)> group-key, name, Unit(ast))
         else
           key := raw-key
         end

  cstx--initial-state(|_, _): () -> Changed() // c.q. added
  cstx--initial-state(|prev-ast, cur-ast): result -> Cached()
    with prev-ast' := <strip-annos> prev-ast
       ; cur-ast'  := <strip-annos> cur-ast
    where(<eq> (prev-ast', cur-ast'))
  cstx--initial-state(|_, _): result -> Changed()

  cstx--unit-term   = ?FileAnalysis(_, _, <id>, _)

  cstx--sub-result(|name): () -> ()
  cstx--sub-result(|name): SubAnalysis(_, _, _, sub-analysis) -> sub-result
    with sub-result := <lookup> (name, sub-analysis) <+ !()

  mk-s-project = !Var("s_prj")
  mk-s-parent  = !Var("s_parentGrp")
  mk-s-group   = !Var("s_grp")

rules // cstx -- project/group tasks

  cstx--make-project(|grp-c-name, libs): (project-task, file-task*) -> Project(project-resource, project-rule, project-res, groups, units, libs)
    with ProjectTask(project-resource, project-rule, project-res) := project-task
       ; (groups, units) := <cstx--make-subs(|project-resource, grp-c-name)> file-task*

  cstx--make-subs(|grp-res, grp-c-name): file-task* -> (groups, units)
    with key* := <filter({key: \ ([key|_], _) -> key \}); make-set> file-task*
       ; <cstx--keys-consistent> key*
       ; file-sub-task* := <filter(?([], FileTask(_, _, _, _)))> file-task*
       ; file-sub* := <map({ resource, name : ?([], FileTask(resource, Key(_, name, _), _, <id>)); cstx--unit-task-to-sub(|resource, name) })> file-sub-task*
       ; group-sub* := <map({key, result: \ key -> result
           with result := <filter({ keys, file: ?([key|keys], file); !(keys, file) })
                ; cstx--make-sub(|key, grp-res, grp-c-name)
                > file-task*
         \ })> key*
       ; subs   := [file-sub*, group-sub*]
       ; groups := <filter(?(_, Group(_, _, _, _, _)))> subs
       ; units  := <filter(?(_, Unit(_, _, _)))> subs

  // Every name maps to same term
  cstx--keys-consistent = ?keys; map(?GroupKey(<id>, _)); make-set; map({ n: \ n -> n with <filter(?GroupKey(n, <id>)); make-set> keys => [_] \})

  cstx--make-sub(|key, parent-res, grp-c-name): file-task* -> grp
    with GroupKey(grp-id, _) := key
       ; grp-res := $[[parent-res]/[grp-id]]
       ; grp     := <cstx--make-group(|key, grp-res, grp-c-name)> file-task*

  cstx--make-group(|key, grp-res, grp-c-name): file-task* -> (grp-id, Group(grp-res, rule, Cached(), groups, units))
    with GroupKey(grp-id, _) := key
       ; s-proj           := Var("s_proj")
       ; s-this-grp       := Var("s_thisGrp")
       ; rule             := LLam([s-proj, s-this-grp], C(grp-c-name, [s-proj, <?GroupKey(_, <id>); stx--explode> key, s-this-grp], NoMessage()))
       ; (groups, units)  := <cstx--make-subs(|grp-res, grp-c-name)> file-task*

  cstx--unit-task-to-sub(|resource, name): SimpleFileTask(constraint, state) -> (name, unit)
    with unit := Unit(resource, constraint, state)

  cstx--unit-task-to-sub(|resource, name): CompositeTask(constraint, state, subtask*) -> (name, Group(resource, constraint, state, group*, unit*))
    with sub*     := <map({name', task': \ (name', task') -> <cstx--unit-task-to-sub(|$[[resource]/[name']], name')> task' \})> subtask*
       ; group*   := <filter(?(_, Group(_, _, _, _, _)))> sub*
       ; unit*    := <filter(?(_, Unit(_, _, _)))> sub*

  // @param spec  : Spec
  // @param level : LogLevel
  // @type StatixProject -> [(String, SolverResult)]
  cstx--solve-multi(|solver-mode, spec, level, progress, cancel) =
    prim("STX_solve_multi", solver-mode, spec, level, progress, cancel)

rules // cstx -- results

  cstx--mk-project-result(|config, analysis*): (project-res, ast, change) -> (project-res, result)
    with if ProjectResult(state, unit-result) := <lookup> (project-res, analysis*) then
         (error*, warning*, note*) := <stx--delays-as-errors(|config); stx--extract-messages(|config)> state
       ; analysis := ProjectAnalysis(state, unit-result)
       ; switch !change
           case ?Added(_) + ?Changed(_, _):
             result := Full(ast, analysis, error*, warning*, note*)
           case ?Cached(_):
             result := Update(analysis, error*, warning*, note*)
       end
    else
      result := Failed()
    end

  cstx--mk-file-result(post|config, file-task*, analysis*): (resource, change) -> (resource, result)
    with task@FileTask(_, key, ast, sub-task) := <getfirst(?(_, FileTask(resource, _, _, _))); Snd> file-task*
       ; if (sub-result, error*, warning*, note*) := <lookup; cstx--mk-subfile-result(|config, analysis*, sub-task, resource)> (resource, analysis*) then
           analysis := FileAnalysis(key, task, ast, sub-result)
         ; switch !change
             case ?Added(_) + ?Changed(_, _):
               result := Full(<post> ast, analysis, error*, warning*, note*)
             case ?Cached(_):
               result := Update(analysis, error*, warning*, note*)
            end
         else
           result := Failed()
         end

  cstx--mk-subfile-result(|config, analysis*, task, resource): GroupResult(state, unit-result) -> result'
    with CompositeTask(constraint, _, sub-task*) := task
       ; (error*, warning*, note*) := <stx--delays-as-errors(|config); stx--extract-messages(|config)> state
       ; sub-result* := <map({ name, sub-task, sub-result: \ (name, sub-task) -> (name, sub-result, error', warning', note')
           with (sub-result, error', warning', note') := <cstx--mk-subfile-result(|config, analysis*, sub-task, $[[resource]/[name]])> sub-task
         \ })> sub-task*
       ; sub-analysis* := <map({ name, sub-result: \ (name, sub-result, _, _, _) -> (name, sub-result) \ })> sub-result*
       ; sub-error*    := <mapconcat(Third)> sub-result*
       ; sub-warning*  := <mapconcat(Ttl; Third)> sub-result*
       ; sub-note*     := <mapconcat(Ttl; Ttl; Third)> sub-result*
       ; result        := SubAnalysis(constraint, state, unit-result, sub-analysis*)
       ; result'       := (result, [error*, sub-error*], [warning*, sub-warning*], [note*, sub-note*])
    //

  cstx--mk-subfile-result(|config, analysis*, sub-task, resource): UnitResult(state, unit-result) -> (result, error*, warning*, note*)
    with SimpleFileTask(constraint, _) := sub-task
       ; (error*, warning*, note*) := <stx--delays-as-errors(|config); stx--extract-messages(|config)> state
       ; result := SubAnalysis(constraint, state, unit-result, [])

rules // multi file analysis

  stx--editor-analyze(pre, post|spec-name, project-constraint, file-constraint):
      AnalyzeMulti((project-resource, project-change), change*, progress, cancel) -> AnalysisResult(result*)
    with spec  := <stx--language-spec-by-name> spec-name
       ; level := <stx--log-level>
       ; file-c-name := <stx--mk-qualified-name(|spec-name)> file-constraint
       ; proj-c-name := <stx--mk-qualified-name(|spec-name)> project-constraint
       ; config := <stx--project-config>
    with
      switch !project-change
        case ?Added(_) + ?Changed(_, _):
          //
          // 1. create global scope
          //
          global-scope-var := Var("s")
        ; Solution(global-scope-subst, global-scope-state) :=
              <stx--solve-constraint(|spec, level, progress, cancel)> <stx--exists>([global-scope-var], CNew([global-scope-var]))
        ; global-scope := <lookup> (global-scope-var, global-scope-subst)
          //
          // 2. partially solve project constraint
          //
        ; project-C := C(proj-c-name, <map(stx--explode)> [global-scope], NoMessage())
        ; [initial-project-state] := <stx--solve-multi-file(|spec, global-scope-state, level, progress, cancel)> [(project-resource, project-C)]
        case ?Cached(ProjectAnalysis(global-scope, global-scope-state, initial-project-state, _)):
          id
        otherwise:
          fatal-err(|"Unexpected change on project")
      end
      //
      // 3. Partially solve file constraints
      //
    ; resource-ast* := <filter((id, ?Added(<id>) + ?Changed(<id>, _)))> change*
    ; resource-ast'* := <map(\ (resource, ast) -> (resource, <pre;stx--index-ast(|resource)> ast) \)> resource-ast*
    ; file-C* := <map(\ (resource, ast') -> (resource, C(file-c-name, <map(stx--explode)> [global-scope, ast'], NoMessage())) \)> resource-ast'*
    ; initial-file-state* := <stx--solve-multi-file(|spec, global-scope-state, level, progress, cancel)> file-C*
    ; initial-file-result* := <zip({ file-state', ast'', file-analysis: \
        ((resource, ast'), file-state) ->
            (resource, Full(ast'', file-analysis, [], [], []))
          with ast'' := <post> ast'
             ; file-analysis := FileAnalysis(file-state, None())
      \})> (resource-ast'*, initial-file-state*)
      //
      // 4. Combine and solve all partial solution
      //
    ; initial-cached-state* := <filter(?(_, Cached(FileAnalysis(<id>, _))))> change*
    ; final-project-state := <stx--solve-multi-project(|spec, initial-project-state, level, progress, cancel)> [initial-file-state*, initial-cached-state*]
    ; (error*, warning*, note*) := <stx--delays-as-errors(|config);stx--extract-messages(|config)> final-project-state
    ; project-analysis := ProjectAnalysis(global-scope, global-scope-state, initial-project-state, final-project-state)
    ; file-result* := <map((id, Full(id, FileAnalysis(id, !final-project-state), id, id, id)))> initial-file-result*
    ; switch !project-change
        case ?Added(project-ast) + ?Changed(project-ast, _):
          project-result := Full(project-ast, project-analysis, error*, warning*, note*)
        case ?Cached(_):
          project-result := Update(project-analysis, error*, warning*, note*)
      end
    ; result* := [(project-resource, project-result)|file-result*]
      //
      // 5. Compute scope graph diff
      //
    //; if Cached(ProjectAnalysis(_, _, _, final-project-state')) := project-change then
    //    diff := <stx--diff-scopegraphs(|global-scope)> (final-project-state, final-project-state')
    //  ; <pp-Statix-string(prettyprint-Statix-Stx-Diff);debug> diff
    //  end

  stx--editor-analyze(pre, post|spec-name, init-constraint):
      AnalyzeSingle(_, _, _) -> <fatal-err-msg(|"Multi-file `stx-editor-analyze_1_3` called for single-file analysis. Use `stx-editor-analyze_1_2` instead.")>

  stx--prepare-project(|project-constraint, group-constraint, file-constraint) = fail

  // @param state : SolverResult
  // @param level : LogLevel
  // @type [(String, Constraint)] -> [SolverResult]
  stx--solve-multi-file(|spec, state, level, progress, cancel) = prim("STX_solve_multi_file", spec, state, level, progress, cancel)

  // @param state : SolverResult
  // @param level : LogLevel
  // @type [SolverResult] -> SolverResult
  stx--solve-multi-project(|spec, state, level, progress, cancel) = prim("STX_solve_multi_project", spec, state, level, progress, cancel)

  // @type SolverResult -> SolverResult
  stx--delays-as-errors(|config) = <prim("STX_delays_as_errors")> WithConfig(config, <id>)

  // @type SolverResult -> (SolverResult, [Error], [Warning], [Note])
  stx--extract-messages(|config) = <prim("STX_extract_messages")> WithConfig(config, <id>)


  // @type _ -> Config
  stx--project-config = prim("STX_project_config")

  // @type _ -> SolverMode
  stx--solver-mode = prim("STX_solver_mode")


  stx--default-group-key: (resource, _) -> [resource]

rules

  stx--evaluate(|spec-name, constraint):
      in* -> (out, s, error*, warning*, note*)
    with spec  := <stx--language-spec-by-name> spec-name
       ; config := <stx--project-config>
       ; c-name := <stx--mk-qualified-name(|spec-name)> constraint
       ; T*    := <map(stx--explode)> in*
       ; V     := Var("out")
       ; C     := <stx--exists>([V], C(c-name, [T*, V], NoMessage()))
       ; level := <stx--log-level>
       ; s@Solution(subst, a) := <stx--solve-constraint(|spec, level, (), ())> C
       ; (error*, warning*, note*) := <stx--delays-as-errors(|config);stx--extract-messages(|config)> a
       ; out := <substitute(stx--is-var);stx--implode> (subst, V)

rules

  stx--log-level = stx--is-debug-custom-enabled < !"Info" + !"None"


  stx--language-spec-by-name =
    MkSingleton
  ; language-resources(stx--module-path, stx--spec-imports)
  ; map(Snd)
  ; stx--merge-spec-aterms

  stx--local-spec-from-spec =
    ?spec
  ; stx--spec-imports
  ; project-resources(stx--module-path, stx--spec-imports)
  ; map(Snd)
  ; ![spec|<id>]
  ; stx--merge-spec-aterms


  stx--local-libs =
    !["stxlibs"]
  ; project-resources(stx--lib-path, is-list;filter(is-string))
  ; filter(not(?("stxlibs", _)))
 <+ ![]

  stx--lib-path: "stxlibs" -> "lib/stxlibs"
  stx--lib-path: name -> $[lib/[name].stxlib]


  stx--spec-imports = ?FileSpec(<id>, _, _, _, _, _)


  stx--mk-qualified-name(|""): name -> name

  stx--mk-qualified-name(|qname): name -> name'
    with if <elem> ("!", <explode-string> name) then
           name' := name
         else
           name' := $[[qname]![name]]
         end


  stx--merge-spec-aterms = foldr(stx--merge-spec-aterms-init, stx--merge-spec-aterms-step)

  stx--merge-spec-aterms-init = !Spec([], [], EOP(), [], [])

  stx--merge-spec-aterms-step: (spec1, spec) -> spec'
    with FileSpec(_, edge1*, data1*, eop, rule1*, ext1*) := spec1
       ; Spec(edge*, data*, eop, rule*, ext*) := spec
       ; spec' := Spec( <union> (edge1*, edge*)
                      , <union> (data1*, data*)
                      , eop
                      , <conc> (rule1*, rule*)
                      , <union> (ext1*, ext*)
                      )

rules

  stx--module-path: name -> filename
    with filename := <stx--src-gen-file(|"statix", ".spec.aterm")> name

  stx--src-gen-file(|sub-dir, ext): name -> filename
    with filename := $[[<stx--src-gen-dir(|sub-dir)>]/[name][ext]]

  stx--src-gen-dir(|sub-dir): _ -> dirname
    with dirname := <project-srcgen-dir> sub-dir


rules

  // solve-constraint(|Spec, LogLevel) : ([Var], Constraint) -> Solution
  stx--solve-constraint(|spec, level) = stx--solve-constraint(|spec, level, (), ())

  // solve-constraint(|Spec, LogLevel, Progress, Cancel) : ([Var], Constraint) -> Solution
  stx--solve-constraint(|spec, level, progress, cancel) = prim("STX_solve_constraint", spec, level, progress, cancel)

rules

  cstx--solve-constraint(|spec, level) = cstx--solve-constraint(|spec, level, (), ())

  // solve-constraint(|Spec, LogLevel, Progress, Cancel) : ([Var], Constraint) -> Solution
  cstx--solve-constraint(|spec, level, progress, cancel) = prim("STX_solve_constraint_concurrent", spec, level, progress, cancel)

rules

  stx--get-ast-analysis =
    stx--get-ast-resource
  ; stx--get-resource-analysis

  stx--get-resource-analysis =
    get-resource-constraint-analysis
  ; stx--get-result-analysis

  stx--get-project-analyses =
    get-project-constraint-analyses
  ; map((id, stx--get-result-analysis))

  stx--get-result-analysis =
    ( ?FileAnalysis(_, <id>) + ?ProjectAnalysis(_, _, _, <id>)
   <+ ?FileAnalysis(_, _, _, SubAnalysis(_, <id>, _, _)) + ?ProjectAnalysis(<id>, _) // cstx
    )


  stx--is-debug-custom-enabled =
    prim("SG_is_debug_custom_enabled")

  stx--is-concurrent-enabled =
    prim("STX_is_concurrent_enabled")

rules

  /**
   * @param a : Analysis
   * @type : (Scope * Label) -> [Term]
   */
  stx--get-scopegraph-data(|a) =
    prim("STX_get_scopegraph_data", a)

  /**
   * @param a : Analysis
   * @type : (Scope * Label) -> [Scope]
   */
  stx--get-scopegraph-edges(|a) =
    prim("STX_get_scopegraph_edges", a)

  /**
   * @type : Analysis -> Analysis
   */
  stx--is-analysis =
    prim("STX_is_analysis")

  /**
   * @type : Analysis -> Analysis
   */
  stx--analysis-has-errors =
    prim("STX_analysis_has_errors")

  /**
   * @type : Analysis -> ScopeGraph
   */
  stx--debug-scopegraph =
    prim("STX_debug_scopegraph")

  /**
   * @type : Analysis -> ScopeGraph
   */
  stx--get-scopegraph =
    prim("STX_get_scopegraph")

  stx--diff-scopegraphs(|s0) =
    prim("STX_diff_scopegraphs",s0)

  stx--get-occurrence-ns: StxOccurrence(ns, _, _) -> ns

  stx--get-occurrence-terms: StxOccurrence(_, ts, _) -> ts

rules // debug

  stx--debug-log(|msg) = id <+ debug(!msg)
