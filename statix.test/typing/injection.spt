module typing/injection

language StatixLang

fixture [[
  module test
  signature
    sorts A constructors
      A : A
        : B => A
    sorts B constructors
      B : B
  [[...]]
]]

test equating variable with variable of implicitly injected sort fails [[
  rules
    p : A * B
    p(a, b) :- a == b.
]] analysis fails

test equating variable with variable of explicitly injected sort succeeds [[
  rules
    p : A * B
    p(a, b) :- a == b:A.
]] analysis succeeds

test equating variable with implicitly injected constructor fails [[
  rules
    p : A
    p(a) :- a == B().
]] analysis fails

test equating variable with explicitly injected constructor succeeds [[
  rules
    p : A
    p(a) :- a == B():A.
]] analysis succeeds

test variable argument of implicitly injected sort fails [[
  rules
    p : B
    p(b) :- q(b).
  rules
    q : A
]] analysis fails

test variable argument of explicitly injected sort succeeds [[
  rules
    p : B
    p(b) :- q(b:A).
  rules
    q : A
]] analysis succeeds

test constructed argument of implicitly injected sort fails [[
  rules
    p :
    p() :- q(B()).
  rules
    q : A
]] analysis fails

test constructed argument of explicitly injected sort succeeds [[
  rules
    p :
    p() :- q(B():A).
  rules
    q : A
]] analysis succeeds

test variable parameter of explicitly injected sort succeeds [[
  rules
    p : A
    p(b:B) :- q(b).
  rules
    q : B
]] analysis succeeds

test constructed parameter of explicitly injected sort succeeds [[
  rules
    p : A
    p(B():B).
  rules
    q : B
]] analysis succeeds

test functional predicate with explicitly injected output succeeds [[
  rules
    q : -> A
    q() = B():A.
]] analysis succeeds
